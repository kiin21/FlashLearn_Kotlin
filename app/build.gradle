plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.ksp)
    alias(libs.plugins.hilt)
    alias(libs.plugins.google.services)
}



android {
    namespace = "com.kotlin.flashlearn"
    compileSdk = 34

    defaultConfig {
        applicationId = "com.kotlin.flashlearn"
        minSdk = 24
        targetSdk = 34
        versionCode = 1
        versionName = "1.0"

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary = true
        }
    }

    buildTypes {
        release {
            minifyEnabled = false
            proguardFiles(getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro")
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        compose = true
        buildConfig = true
    }
    composeOptions {
        kotlinCompilerExtensionVersion = libs.versions.composeCompiler.get()
    }
    packaging {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    
    // Auto-extract web_client_id from google-services.json
    defaultConfig {
        def googleServicesFile = file("google-services.json")
        if (googleServicesFile.exists()) {
            def json = new groovy.json.JsonSlurper().parse(googleServicesFile)
            def webClientId = ""
            json.client.each { client ->
                client.oauth_client?.each { oauth ->
                    if (oauth.client_type == 3) {
                        webClientId = oauth.client_id
                    }
                }
            }
            buildConfigField("String", "WEB_CLIENT_ID", "\"${webClientId}\"")
        }
        
        // API Configuration
        def props = new Properties()
        rootProject.file('local.properties').with { if (it.exists()) props.load(it.newInputStream()) }
        
        def neonConnectionString = props.getProperty('NEON_CONNECTION_STRING')
        def neonSqlBaseUrl = props.getProperty('NEON_SQL_BASE_URL')
        
        // Auto-extract hostname from connection string if available
        if (neonConnectionString) {
             // Extract hostname between '@' and next '/' or ':'
             def matcher = (neonConnectionString =~ /@([^:\/]+)/)
             if (matcher.find()) {
                 neonSqlBaseUrl = "https://${matcher.group(1)}/"
             }
        }

        buildConfigField("String", "DATAMUSE_BASE_URL", "\"${props.getProperty('DATAMUSE_BASE_URL')}\"")
        buildConfigField("String", "NEON_SQL_BASE_URL", "\"${neonSqlBaseUrl}\"")
        buildConfigField("String", "NEON_CONNECTION_STRING", "\"${neonConnectionString}\"")
        buildConfigField("String", "PIXABAY_API_KEY", "\"${props.getProperty('PIXABAY_API_KEY')}\"")
        
        // Cloudinary
        buildConfigField("String", "CLOUDINARY_CLOUD_NAME", "\"${props.getProperty('CLOUDINARY_CLOUD_NAME', '')}\"")
        buildConfigField("String", "CLOUDINARY_API_KEY", "\"${props.getProperty('CLOUDINARY_API_KEY', '')}\"")
        buildConfigField("String", "CLOUDINARY_API_SECRET", "\"${props.getProperty('CLOUDINARY_API_SECRET', '')}\"")
    }
}

dependencies {
    // Core
    implementation libs.androidx.core.ktx
    implementation libs.androidx.lifecycle.runtime.ktx
    implementation libs.androidx.lifecycle.runtime.compose
    implementation libs.androidx.lifecycle.viewmodel.compose
    implementation libs.androidx.activity.compose
    implementation("androidx.appcompat:appcompat:1.6.1")

    // Compose
    implementation platform(libs.androidx.compose.bom)
    implementation libs.androidx.ui
    implementation libs.androidx.ui.graphics
    implementation libs.androidx.ui.tooling.preview
    implementation libs.androidx.material3
    implementation libs.androidx.material.icons.extended

    // Navigation
    implementation libs.androidx.navigation.compose

    // Hilt
    implementation libs.hilt.android
    ksp libs.hilt.android.compiler
    implementation libs.hilt.navigation.compose

    // Firebase & Google
    implementation platform(libs.firebase.bom)
    implementation libs.firebase.firestore
    implementation libs.firebase.auth.ktx
    implementation libs.play.services.auth
    
    // Cloudinary for image uploads
    implementation("com.cloudinary:cloudinary-android:2.5.0")

    // Image Loading
    implementation libs.coil.compose

    // Networking
    implementation libs.retrofit
    implementation libs.retrofit.gson
    implementation libs.okhttp
    implementation libs.okhttp.logging
    implementation libs.gson

    // Room Database
    implementation libs.room.runtime
    implementation libs.room.ktx
    ksp libs.room.compiler

    // WorkManager
    implementation libs.workmanager.runtime
    implementation libs.hilt.work
    ksp libs.hilt.work.compiler

    // Auth Utils
    implementation("org.mindrot:jbcrypt:0.4")
    implementation("com.fasterxml.uuid:java-uuid-generator:5.0.0")

    // Testing
    testImplementation libs.junit
    androidTestImplementation libs.androidx.junit
    androidTestImplementation libs.androidx.espresso.core
    androidTestImplementation platform(libs.androidx.compose.bom)
    androidTestImplementation libs.androidx.ui.test.junit4
    debugImplementation libs.androidx.ui.tooling
    debugImplementation libs.androidx.ui.test.manifest
}